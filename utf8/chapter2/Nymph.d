// Лес Невервинтер, дом нимфы - Нимфа в начале разговора пытается очаровать. Запустить диалог, потом катсцену, потом продолжить диалог. 

BEGIN ~NYMPH~

IF WEIGHT #0 /* Triggers after states #: 9 even though they appear after this state */
~  NumTimesTalkedTo(0)Global("Sharm","LOCALS",0)~ THEN BEGIN 0 // from:
  SAY ~Нет, я не позволю тебе убить меня! Создания природы должны быть защищены, подчинись моей воле!~
  IF ~~ THEN DO ~SetGlobal("Sharm","LOCALS",1)ClearAllActions()StartCutSceneMode()StartCutScene("Sharm")~ EXIT
END

IF WEIGHT #1 /* Triggers after states #: 9 even though they appear after this state */
~  Global("Sharm","LOCALS",1)Global("KnowNymph","LOCALS",0)~ THEN BEGIN 1 // from:
  SAY ~Ну ты и тупица, <MALEFEMALE>! Я не знаю, с чего тебе взбрело в голову, что ты можешь залезть в мой дом, убивать моих рабов и уничтожать других защитников природы, но теперь ты - жертва моих чар. Склонись же перед своей госпожой!~
  IF ~~ THEN REPLY ~О чем ты говоришь? Я не раб.~ DO ~SetGlobal("Sharm","LOCALS",2)~ GOTO 2
  IF ~~ THEN REPLY ~Да, госпожа. Что прикажете?.~ DO ~SetGlobal("Sharm","LOCALS",2)~ GOTO 3
  IF ~~ THEN REPLY ~Если ты пыталась подчинить меня своими чарами, ты проиграла. Попытайся еще раз, если хочешь умереть..~ DO ~SetGlobal("Sharm","LOCALS",2)~ GOTO 4
  IF ~~ THEN REPLY ~Я тебе не раб, мерзкое создание. Готовься к смерти..~ DO ~SetGlobal("Sharm","LOCALS",2)~ GOTO 5
END

IF ~~ THEN BEGIN 2 // from: 1.1
  SAY ~Не отвечай мне, раб! Так, какую бы еще грязную работу тебе поручить?~
  IF ~~ THEN REPLY ~Если ты пыталась подчинить меня своими чарами, ты проиграла. Попытайся еще раз, если хочешь умереть.~ GOTO 4
  IF ~~ THEN REPLY ~Я тебе не раб, мерзкое создание. Готовься к смерти.~ GOTO 5
END

IF ~~ THEN BEGIN 3 // from: 1.2
  SAY ~Во-первых, иди и убери все это безобразие, которое осталось после твоей драки с моими рабами. После этого собери все ночные вазы и вычисти их. Перед тем, как ты уйдешь, я приказываю тебе надеть этот миленький костюмчик.~
  IF ~~ THEN REPLY ~Грязную работу? Я так не думаю!~ GOTO 6
  IF ~~ THEN REPLY ~Выносить твои ночные вазы? Даже не думай, это отвратительно.~ GOTO 6
  IF ~~ THEN REPLY ~Я такого не ношу! Оно розовое!!!~ GOTO 6
END

IF ~~ THEN BEGIN 4 // from: 1.3 2.1
  SAY ~Нет! Как это может быть? Что случилось с моими силами?~
  IF ~~ THEN REPLY ~Ты и твои рабы напали на меня. Приведи мне хоть одну причину, почему не стоит тебя убивать?~ GOTO 7
  IF ~~ THEN REPLY ~Да мне вообще плевать на твои силы. Их нет, а сейчас отвечай, что здесь творится?~ GOTO 7
END

IF ~~ THEN BEGIN 5 // from: 1.4 2.2
  SAY ~Нет, пожалуйста! Пожалуйста, не мучай меня. Что я такого сделала, чтобы заслужить это нападение на мой дом?~
  IF ~~ THEN REPLY ~Ты первая начала! Это была не моя идея.~ GOTO 7
  IF ~~ THEN REPLY ~Твои рабы напали на меня, потом ты сделала то же самое. Этого не достаточно?~ GOTO 7
  IF ~~ THEN REPLY ~Ты это начала, а теперь я с этим покончу!~ GOTO 8
END

IF ~~ THEN BEGIN 6 // from: 3
  SAY ~Что? Как ты смеешь возражать мне! Ты же мой безмозглый ра... ты... ты обманываешь меня! Нет! Как это может быть? Что случилось с моими силами?~
  IF ~~ THEN REPLY ~Ты и твои рабы напали на меня. Приведи мне хоть одну причину, почему не стоит тебя убивать?~ GOTO 7
  IF ~~ THEN REPLY ~Да мне вообще плевать на твои силы. Их нет, а сейчас отвечай, что здесь творится?~ GOTO 7
END

IF ~~ THEN BEGIN 7 // from: 4.1
  SAY ~Ты... разве ты здесь не для того, чтобы убить меня?~
  IF ~  GlobalGT("AavillQuest","GLOBAL",0)~ THEN REPLY ~Конечно, нет! Меня послали сюда выяснить, чем можно помочь Духу Леса.~ GOTO 9
  IF ~~ THEN REPLY ~Я прихожу сюда поговорить, а на меня бросаются, не дав слова сказать. С чего ты взяла, что я убийца?~ GOTO 9
END

IF ~~ THEN BEGIN 8 // from: 4.2
  SAY ~Тогда я взываю к силам природы, чтобы защитить себя.~
  IF ~~ THEN DO ~Enemy()ClearAllActions()StartCutSceneMode()StartCutScene("NymphSum")~ EXIT
END

IF ~~ THEN BEGIN 9 // from: 4
  SAY ~О, это ужасно. Мне так жаль, но я была уверена, что ты... о, я сейчас уже и не знаю, почему так подумала. Я ничего не могу сделать, чтобы это исправить. Я была неправа; как это могло случиться?~
  IF ~~ THEN GOTO 10
END

IF ~~ THEN BEGIN 10 // from: 9
  SAY ~Ты здесь, чтобы убить меня. Вот что пришло мне в голову в тот момент, как почувствовалось твое присутствие рядом с моим деревом. Мне... мне просто хотелось остановить тебя. Ты сможешь меня простить?~
  IF ~~ THEN REPLY ~Полагаю, я могу тебя простить, но теперь я ожидаю от тебя всяческой помощи.~ GOTO 11
  IF ~~ THEN REPLY ~Я не могу тебя простить, но не буду трогать, если ты только еще что-нибудь не выкинешь.~ GOTO 12
  IF ~~ THEN REPLY ~Тому, что ты сделала, нет прощения, это может искупить лишь смерть.~ GOTO 8
END

IF ~~ THEN BEGIN 11 // from: 10.1
  SAY ~О, спасибо, <LADYLORD>! Это так великодушно. Я сделаю все, что смогу, чтобы помочь тебе. Вот только жаль, что сил у меня уже почти нет - не знаю, как получилось, что они так иссякли.~
  IF ~~ THEN REPLY ~Как долго у тебя были эти проблемы?~ GOTO 13
  IF ~~ THEN REPLY ~Но чтобы управлять рабами, силы у тебя хватило.~ GOTO 14
END

IF ~~ THEN BEGIN 12 // from: 10.2
  SAY ~Я сделаю, как ты говоришь. Хотела бы я, чтобы был какой-то способ тебе помочь. Но без моих сил я мало что могу сделать.~
  IF ~~ THEN REPLY ~Как долго у тебя были эти проблемы?~ GOTO 13
  IF ~~ THEN REPLY ~Но чтобы управлять рабами, силы у тебя хватило.~ GOTO 14
END

IF ~~ THEN BEGIN 13 // from: 11.1
  SAY ~Пару недель назад я была внизу, у реки, смотрела, как рыба играет, и вдруг почувствовала какую-то опасность. Я огляделась, затем вскарабкалась вверх по берегу и укрылась в роще деревьев. Я ничего не видела, но страх нарастал. Внезапно в моей голове раздался крик, как будто кто-то вонзил кинжал прямо мне в мозг. Его сила так поразила меня, что я упала на землю без сознания. Когда я очнулась, страх был все еще здесь, а еще чувство, будто весь лес испытывает боль.~
  IF ~~ THEN REPLY ~Что же могло быть причиной?~ GOTO 15
END

IF ~~ THEN BEGIN 14 // from: 11.2
  SAY ~Немного. Моя способность управлять ими очень ослабла в последнее время.~
  IF ~~ THEN GOTO 13
END

IF ~~ THEN BEGIN 15 // from: 13.1
  SAY ~Это был Дух Леса. С того дня я всегда чувствовала его боль и страх. Он постоянно кричит о помощи. Я делала все, чтобы удержать свой рассудок; воистину, думаю, тебе пришлось на своей шкуре испытать мой проигрыш в этой битве. С течением времени я начала держаться все ближе к дому, пока недавно не обнаружила, что вообще не могу покинуть свою комнату, так велик мой страх.~
  IF ~~ THEN REPLY ~Если все так плохо, почему ты тогда не покинула это место?~ GOTO 16
  IF ~~ THEN REPLY ~Здорово же тебе досталось. Я думаю, у каждого бы возникли проблемы с рассудком.~ GOTO 17
  IF ~~ THEN REPLY ~Какая же ты хлипкая, если так легко сдалась и начала обращать других в рабов.~ GOTO 18
END

IF ~~ THEN BEGIN 16 // from: 15.1
  SAY ~Не все так просто, как кажется. Это дерево - мой дом, я живу здесь уже столетия. Однако у меня было такое чувство, будто каждый, кто проходит мимо, представляет опасность для моего дома и меня самой.~
  IF ~~ THEN GOTO 19
END

IF ~~ THEN BEGIN 17 // from: 15.2
  SAY ~Твои слова очень приятно слышать. Я... я чувствую, как будто ты даешь мне силу. Пока я говорю с тобой, страх не такой всепоглощающий. Ох... ладно, думаю, я могу тебе еще кое-что рассказать.~
  IF ~~ THEN GOTO 19
END

IF ~~ THEN BEGIN 18 // from: 15.3
  SAY ~Тебе никогда не приходилось чувствовать у себя в голове что-то, что влияет на твои эмоции и пытается заставить тебя подчиняться ему. Ты так презираешь меня, может, мне стоит... Не важно. Прошу прощения за этот срыв. Мне продолжать?~
  IF ~~ THEN REPLY ~Извини. Полагаю, это у меня все из-за того шока, когда вы напали на меня. Продолжай.~ GOTO 19
  IF ~~ THEN REPLY ~Просто давай дальше и постарайся хныкать поменьше.~ GOTO 19
  IF ~~ THEN REPLY ~Мне надоело твое нытье, хватит с меня. До свидания.~ GOTO 20
END

IF ~~ THEN BEGIN 19 // from: 16.1 17.1 18.1 18.2
  SAY ~В первый раз, когда человек подошел близко к моему дереву, я захватила его своей силой и заставила охранять мой дом. Следующие несколько тоже дались легко, но потом было все труднее и труднее. Теперь, как видишь, мои силы иссякли. Не знаю, что я дальше буду делать.~
  IF ~~ THEN REPLY ~Чтобы тебе помочь, мне нужно добраться до Духа Леса. Ты знаешь, как я могу это сделать?~ GOTO 21
  IF ~  GlobalGT("AavillQuest","GLOBAL",0)~ THEN REPLY ~Чтобы выполнить задание, мне надо добраться до Духа Леса. Ты можешь мне помочь?~ GOTO 21
END

IF ~~ THEN BEGIN 20 // from: 18.3
  SAY ~Я... я действительно жалею о том, что сделала. Если тебе что-нибудь еще понадобится, пожалуйста, возвращайся.~
  IF ~~ THEN DO ~SetGlobal("KnowNymph","LOCALS",1)~ EXIT
END

IF ~~ THEN BEGIN 21 // from: 19
  SAY ~Я не думаю, что это возможно. Дух - создание одновременно и жизни, и смерти, и только он может войти в портал, ведущий в иную реальность.~
  IF ~~ THEN REPLY ~Что это значит - и жизнь, и смерть?~ GOTO 22
  IF ~~ THEN REPLY ~Должен же быть способ! Где этот портал?~ GOTO 23
END

IF ~~ THEN BEGIN 22 // from: 19
  SAY ~Дух живет в иной реальности, где-то между миром живых и миром мертвых. Это единственное создание на этом плане, которое может войти в портал.~
  IF ~~ THEN GOTO 23
END

IF ~~ THEN BEGIN 23 // from: 21.2
  SAY ~Портал в пруду, под водопадом, но то, что ты его найдешь, тебе не поможет. Да и никому не поможет.~
  IF ~~ THEN REPLY ~И ты уверена, что кроме Духа туда никто не входил?~ GOTO 24
  IF ~~ THEN REPLY ~Должен же быть способ для меня попасть туда.~ GOTO 24
END

IF ~~ THEN BEGIN 24 // from: 23
  SAY ~Ну... некоторые пытались... но нет... плохое решение. Забудь об этом. Только Дух может проделывать это успешно.~
  IF ~~ THEN REPLY ~Ты собиралась сказать что-то еще. Пожалуйста, ты должна мне ответить.~ GOTO 25
  IF ~~ THEN REPLY ~Ну-ка рассказывай, что начала говорить, а то пожалеешь.~ GOTO 26
END

IF ~~ THEN BEGIN 25 // from: 24.1
  SAY ~Давным-давно был друид, который утверждал, что понял, как проникнуть в реальность Духа. Он говорил, что изучал головоломку много лет, и в конце концов она сложилась. Он не сказал мне, как собирается войти, но я была любопытна и проследила за ним. Он вошел в пруд и построил алтарь, но затем он сделал очень странную вещь! Он вынул кинжал и вонзил его в собственную грудь. Его тело соскользнуло под воду, но когда я подошла к пруду, оно исчезло.~
  IF ~~ THEN GOTO 27
END

IF ~~ THEN BEGIN 26 // from: 24.2
  SAY ~Тебе не нужно угрожать мне, я же сказала, что выполню твои пожелания.~
  IF ~~ THEN GOTO 25
END

IF ~~ THEN BEGIN 27 // from: 25
  SAY ~Может быть, он и попал в ту реальность, но если он был при этом уже мертв, не думаю, что оно того стоило. За прошедшие годы и другие пытались сделать то же самое, но их тела просто лежали там и разлагались. Ну... не все тела.~
  IF ~~ THEN REPLY ~Что ты имеешь в виду, остальные? Скажи, это важно.~ GOTO 28
  IF ~~ THEN REPLY ~Просто скажи мне все, что тебе известно, и прекрати тратить мое время!~ GOTO 28
END

IF ~~ THEN BEGIN 28 // from: 27
  SAY ~Несколько недель назад сюда пришел один дварф. Он был в ярости и мне показалось, что мои чары на него не действуют. Он спросил меня о кинжале, при помощи которого друид убил себя. Я не знаю, как он узнал об этом друиде, но кинжал хранился у меня, как память о нем. Когда дварф начал мне угрожать, мне пришлось отдать ему кинжал.~
  IF ~~ THEN GOTO 29
END

IF ~~ THEN BEGIN 29 // from: 28
  SAY ~С этим дварфом мы никогда больше не встречались, но незадолго до того, как начались все эти беды, я нашла тот самый кинжал перед алтарем.~
  IF ~~ THEN REPLY ~Мне нужен этот кинжал. Он у тебя?~ DO ~EraseJournalEntry(%Лес Невервинтер: Дух Леса

Дух Леса Невервинтер с недавних пор находится в плачевном состоянии, и архидруид Аавилл, который возглавляет конклав, очень обеспокоен этим. С большой неохотой он позволил нам войти в его лес, в надежде, что мы поможем ему выяснить, что же произошло.%)
EraseJournalEntry(%Лес Невервинтер: Дух Леса

Дух Леса Невервинтер с недавних пор находится в плачевном состоянии, и архидруид Аавилл, который возглавляет конклав, очень обеспокоен этим. С большой неохотой он позволил нам войти в лес, в надежде, что мы поможем ему выяснить, что же произошло. Другие друиды в глубине леса говорят, что можно увидеть Духа сначала зайдя, а потом выйдя из лесного пруда, и совершив какой-то ритуал. Однако они не имеют ни малейшего представления о том, что это мог бы быть за ритуал.%)~ UNSOLVED_JOURNAL ~Лес Невервинтер: Дух Леса

Дух Леса Невервинтер был отравлен, и Аавилл, архидруид местного конклава друидов, серьезно обеспокоен. Он дал нам доступ в лес, надеясь, что мы поможем ему выяснить, в чем дело. В глубине леса были обнаружены сведения о существовании пруда у водопада, где Дух Леса проводил ритуал, чтобы попасть в мир духов. Для смертного единственный способ последовать за ним - войти в пруд и умереть в его водах. Для того чтобы лишить смертного жизни, потребуется магический кинжал.~ GOTO 30
  IF ~~ THEN REPLY ~Тело дварфа было в бассейне?~ GOTO 31
END

IF ~~ THEN BEGIN 30 // from: 29.1
  SAY ~Я принесла и его сюда, но я не помню, куда он делся, потому что как только мне удалось добраться до дома, меня пронзила острая боль. Но он точно где-то в моем доме. Если как следует поищешь, наверняка сможешь его найти, но ни за что не используй его! Я не хочу нести ответственность еще за одну смерть, у меня и так уже руки по локоть в крови!~
  IF ~~ THEN REPLY ~Можешь ли ты сказать мне что-нибудь еще полезное?~ GOTO 32
  IF ~~ THEN REPLY ~Все в порядке. Я собираюсь найти Духа и помочь тебе избежать этого ужаса.~ GOTO 33
  IF ~~ THEN REPLY ~Не начинай снова ныть. Соберись хоть немного.~ GOTO 34
  IF ~~ THEN REPLY ~По-моему, я больше не в силах тебя слушать. До свидания.~ GOTO 35
END

IF ~~ THEN BEGIN 31 // from: 29.2
  SAY ~Нет, теперь, когда я об этом думаю, я понимаю, что его трупа там не было.~
  IF ~~ THEN REPLY ~Мне нужен этот кинжал. Он у тебя?~ DO ~EraseJournalEntry(%Лес Невервинтер: Дух Леса

Дух Леса Невервинтер с недавних пор находится в плачевном состоянии, и архидруид Аавилл, который возглавляет конклав, очень обеспокоен этим. С большой неохотой он позволил нам войти в его лес, в надежде, что мы поможем ему выяснить, что же произошло.%)
EraseJournalEntry(%Лес Невервинтер: Дух Леса

Дух Леса Невервинтер с недавних пор находится в плачевном состоянии, и архидруид Аавилл, который возглавляет конклав, очень обеспокоен этим. С большой неохотой он позволил нам войти в лес, в надежде, что мы поможем ему выяснить, что же произошло. Другие друиды в глубине леса говорят, что можно увидеть Духа сначала зайдя, а потом выйдя из лесного пруда, и совершив какой-то ритуал. Однако они не имеют ни малейшего представления о том, что это мог бы быть за ритуал.%)~ UNSOLVED_JOURNAL ~Лес Невервинтер: Дух Леса

Дух Леса Невервинтер был отравлен, и Аавилл, архидруид местного конклава друидов, серьезно обеспокоен. Он дал нам доступ в лес, надеясь, что мы поможем ему выяснить, в чем дело. В глубине леса были обнаружены сведения о существовании пруда у водопада, где Дух Леса проводил ритуал, чтобы попасть в мир духов. Для смертного единственный способ последовать за ним - войти в пруд и умереть в его водах. Для того чтобы лишить смертного жизни, потребуется магический кинжал.~ GOTO 30
END

IF ~~ THEN BEGIN 32 // from: 30.1
  SAY ~По-моему, я больше не знаю ничего полезного, но я отвечу на любые вопросы, которые у тебя есть.~
  IF ~  GlobalGT("Druid3Saved","GLOBAL",0)~ THEN REPLY ~В одной из твоих комнат находился очарованный тобой друид. Ты не знаешь, где остальные друиды?~ GOTO 36
  IF ~  GlobalGT("KnowSetara","GLOBAL",0)~ THEN REPLY ~Что ты знаешь об этой ведьме, Сетаре?~ GOTO 37
  IF ~  Global("SetaraQuest","GLOBAL",1)~ THEN REPLY ~У тебя есть зеркало, которое ищет Сетара?~ GOTO 38
  IF ~~ THEN REPLY ~Ты можешь опять рассказать мне свою историю?~ GOTO 39
  IF ~~ THEN REPLY ~Пока что я больше ничего не могу придумать. Прощай.~ GOTO 40
END

IF ~~ THEN BEGIN 33 // from: 30.2
  SAY ~В тебе таится сила, которая гонит прочь маску ужаса, нависшую надо мной в последние дни. Я надеюсь... конечно, это выглядит как будто я навязываюсь, но я искренне надеюсь, что могу называть тебя другом.~
  IF ~~ THEN GOTO 32
END

IF ~~ THEN BEGIN 34 // from: 30.3
  SAY ~Тебя ведь не очень любят, правда?~
  IF ~~ THEN GOTO 32
END

IF ~~ THEN BEGIN 35 // from: 30.4
  SAY ~Не все ли равно. Сейчас я хотела бы остаться одна. Надеюсь, ты поможешь спасти Духа.~
  IF ~~ THEN DO ~SetGlobal("KnowNymph","LOCALS",1)~ EXIT
END

IF ~~ THEN BEGIN 36 // from: 32.1
  SAY ~Мне и правда жаль, если пропали другие друиды, но я про них ничего не знаю. Эльф был последним, которого я захватила своей силой, перед тем как опробовать ее на тебе.  Я даже не смогла полностью поработить его - приходилось обновлять чары каждый раз, когда я входила в комнату, где он содержался. Я очень жалею о том, что держала его взаперти, но я боялась - когда он поймет, что находился под действием заклинания, он захочет убить меня.~
  IF ~~ THEN GOTO 41
END

IF ~~ THEN BEGIN 41 // from: 36.1
  SAY ~По-моему, я больше не знаю ничего полезного, но я отвечу на любые вопросы, которые у тебя есть.~
  IF ~  GlobalGT("KnowSetara","GLOBAL",0)~ THEN REPLY ~Что ты знаешь об этой ведьме, Сетаре?~ GOTO 37
  IF ~  Global("SetaraQuest","GLOBAL",1)~ THEN REPLY ~У тебя есть зеркало, которое ищет Сетара?~ GOTO 38
  IF ~~ THEN REPLY ~Ты можешь опять рассказать мне свою историю?~ GOTO 39
  IF ~~ THEN REPLY ~Пока что я больше ничего не могу придумать. Прощай.~ GOTO 40
END

IF ~~ THEN BEGIN 37 // from: 32.2
  SAY ~Ох, она ужасно безобразная старая карга. Тебе вообще не захочется иметь с ней дела. Я не могу даже взглянуть на нее без слез, так что я вообще не подпускала ее к моему дереву. Я даже не люблю о ней говорить, так это все меня раздражает. Могу ли я что-нибудь еще сделать?~
  IF ~  GlobalGT("Druid3Saved","GLOBAL",0)~ THEN REPLY ~В одной из твоих комнат находился очарованный тобой друид. Ты не знаешь, где остальные друиды?~ GOTO 36
  IF ~  Global("SetaraQuest","GLOBAL",1)~ THEN REPLY ~У тебя есть зеркало, которое ищет Сетара?~ GOTO 38
  IF ~~ THEN REPLY ~Ты можешь опять рассказать мне свою историю?~ GOTO 39
  IF ~~ THEN REPLY ~Пока что я больше ничего не могу придумать. Прощай.~ GOTO 40
END

IF ~~ THEN BEGIN 38 // from: 32.3
  SAY ~У меня есть магическое зеркало, но оно лишь отражает смотрящего так, как бы он хотел выглядеть, а не как выглядит на самом деле. Конечно, оно не вернет никому молодость. Но я думаю, для такой безобразной карги, как эта ведьма, оно может показаться пределом мечтаний.~
  IF ~~ THEN GOTO 42
END

IF ~~ THEN BEGIN 42 // from: 38.1
  SAY ~Если это тебе поможет на пути к спасению Духа, конечно, ты можешь взять его. Что-нибудь еще тебе нужно?~
  IF ~  GlobalGT("Druid3Saved","GLOBAL",0)~ THEN REPLY ~В одной из твоих комнат находился очарованный тобой друид. Ты не знаешь, где остальные друиды?~ GOTO 36
  IF ~  GlobalGT("KnowSetara","GLOBAL",0)~ THEN REPLY ~Что ты знаешь об этой ведьме, Сетаре?~ GOTO 37
  IF ~~ THEN REPLY ~Ты можешь опять рассказать мне свою историю?~ GOTO 39
  IF ~~ THEN REPLY ~Пока что я больше ничего не могу придумать. Прощай.~ GOTO 40
END

IF ~~ THEN BEGIN 39 // from: 32.4
  SAY ~Мое могущество слабеет, и я все больше и больше впадаю в панику с тех пор, как Дух начал овладевать моим разумом. Я не знаю, из-за чего все эти беды, но если ты найдешь Духа, ты можешь найти ответы. До Духа можно добраться через портал, который скрыт под водопадом здесь, в лесах. Единственным, кто, возможно, прошел через этот портал, был друид, который вошел в пруд и убил себя.~
  IF ~~ THEN GOTO 46
END

IF ~~ THEN BEGIN 46 // from: 39
  SAY ~Но у меня нет настоящих доказательств того, что он добрался туда. Я думаю, очень глупо будет это повторять, ведь мертвые мало что могут сделать. По-моему, я больше не знаю ничего полезного, но я отвечу на любые вопросы, которые у тебя есть.~
  IF ~  GlobalGT("Druid3Saved","GLOBAL",0)~ THEN REPLY ~В одной из твоих комнат находился очарованный тобой друид. Ты не знаешь, где остальные друиды?~ GOTO 36
  IF ~  GlobalGT("KnowSetara","GLOBAL",0)~ THEN REPLY ~Что ты знаешь об этой ведьме, Сетаре?~ GOTO 37
  IF ~  Global("SetaraQuest","GLOBAL",1)~ THEN REPLY ~У тебя есть зеркало, которое ищет Сетара?~ GOTO 38
  IF ~~ THEN REPLY ~Пока что я больше ничего не могу придумать. Прощай.~ GOTO 40
END

IF ~~ THEN BEGIN 40 // from: 32.5
  SAY ~Я желаю тебе удачи в твоем поиске. Я искренне сожалею обо всем, что я тебе сделала, и о смерти моих рабов, в которой следует винить лишь меня.~
  IF ~~ THEN DO ~SetGlobal("KnowNymph","LOCALS",1)~ EXIT
END

// ---------------------------------

IF WEIGHT #2 /* Triggers after states #: 9 even though they appear after this state */
~  Global("KnowNymph","LOCALS",1)Global("SpiritSaved","GLOBAL",0)!Dead("SpiritFo")~ THEN BEGIN 43 // from:
  SAY ~Я вижу, ты возвращаешься. Один лишь вид твоего лица пробуждает в моей душе болезненные воспоминания о смертях, причиной которых стала моя паранойя. Могу ли я что-нибудь для тебя сделать?~
  IF ~  GlobalGT("Druid3Saved","GLOBAL",0)~ THEN REPLY ~В одной из твоих комнат находился очарованный тобой друид. Ты не знаешь, где остальные друиды?~ GOTO 36
  IF ~  GlobalGT("KnowSetara","GLOBAL",0)~ THEN REPLY ~Что ты знаешь об этой ведьме, Сетаре?~ GOTO 37
  IF ~  Global("SetaraQuest","GLOBAL",1)~ THEN REPLY ~У тебя есть зеркало, которое ищет Сетара?~ GOTO 38
  IF ~~ THEN REPLY ~Ты можешь опять рассказать мне свою историю?~ GOTO 39
  IF ~~ THEN REPLY ~Пока что я больше ничего не могу придумать. Прощай.~ GOTO 40
END

// ---------------------------------

IF WEIGHT #3 /* Triggers after states #: 9 even though they appear after this state */
~  Global("KnowNymph","LOCALS",1)GlobalGT("SpiritSaved","GLOBAL",0)~ THEN BEGIN 44 // from:
  SAY ~Герой природы возвращается с триумфом! Я уверена, что твои сила духа и отвага будут вдохновлять всех, кто служит природе.~
  IF ~~ THEN EXIT
END

// -----------------------------------

IF WEIGHT #4 /* Triggers after states #: 9 even though they appear after this state */
~  Global("KnowNymph","LOCALS",1)Dead("SpiritFo")~ THEN BEGIN 45 // from:
  SAY ~Убийца Духа! Я не могу поверить, что ты возвращаешься в Лес, зная, какую беду он из-за тебя пережил. Я больше не буду говорить с тобой!~
  IF ~~ THEN DO ~SetGlobal("KnowNymph","LOCALS",2)~ EXIT
END
