//Choose between placing icons on BP-Worldmap or installing the new NWN Map

DEFINE_ACTION_FUNCTION WORLDMAP_SELECT
  STR_VAR
    worldmap_var = ""
BEGIN
  //BP-Worldmap
  ACTION_MATCH "%worldmap_var%" WITH bp_worldmap BEGIN
    ACTION_IF FILE_EXISTS ~Worldmap/map_mods_areas.tbl~ THEN BEGIN
      COPY ~Worldmap/map_mods_areas.tbl~ ~Worldmap~ APPEND_FILE ~NWNForBG/Worldmap/areas.tbl~
      COPY ~Worldmap/map_mods_trans.tra~ ~Worldmap~ APPEND_FILE ~NWNForBG/tra/autotra/%LANGUAGE%/worldmap.tra~
      ACTION_IF FILE_EXISTS ~Worldmap/map_mods_links.tbl~ THEN BEGIN
        COPY ~Worldmap/map_mods_links.tbl~ ~Worldmap~ APPEND_FILE ~NWNForBG/Worldmap/links.tbl~
      END ELSE BEGIN
        COPY ~NWNForBG/Worldmap/links.tbl~ ~Worldmap/map_mods_links.tbl~
      END
    END ELSE BEGIN
      MKDIR ~Worldmap~
      COPY ~NWNForBG/Worldmap/areas.tbl~ ~Worldmap/map_mods_areas.tbl~
      COPY ~NWNForBG/Worldmap/links.tbl~ ~Worldmap/map_mods_links.tbl~
      COPY ~NWNForBG/tra/autotra/%LANGUAGE%/worldmap.tra~ ~Worldmap/map_mods_trans.tra~
    END
  END
  //New Worldmap for NWN Locations
  nwn_worldmap BEGIN

    //Assign variables
    ACTION_IF GAME_IS ~bg2 tob bgt~ THEN BEGIN
      OUTER_SPRINT worldmap_wmp_name worldmap
      OUTER_SPRINT worldmap_res_loc "%MOD_FOLDER%/worldmap/og/"
      OUTER_SPRINT worldmap_append_bam_loc "%MOD_FOLDER%/tra/%LANGUAGE%/worldmap"
    END
    ACTION_IF GAME_IS ~bg2ee eet~ THEN BEGIN
      OUTER_SPRINT worldmap_wmp_name nwnworld
      OUTER_SPRINT worldmap_res_loc "%MOD_FOLDER%/worldmap/ee/"
      OUTER_SPRINT worldmap_append_bam_loc "%MOD_FOLDER%/tra/%LANGUAGE%/worldmap"
    END

  ACTION_IF FILE_EXISTS EVAL %worldmap_append_bam% THEN BEGIN
    OUTER_SPRINT map_language ~%LANGUAGE%~
  END ELSE BEGIN
    OUTER_SPRINT map_language english
  END

  //Copy WMAP file
  COPY ~%MOD_FOLDER%/worldmap/nwnworld.wmp~ ~override/%worldmap_wmp_name%.wmp~

  //Making dummy files to bypass ADD_MAP_ICONS script protection
  COPY_EXISTING ~sw1h01.itm~ ~override/nwinfo01.are~
  COPY_EXISTING ~sw1h01.itm~ ~override/nwinfo02.are~

  ACTION_IF GAME_IS ~bg2 tob bgt~ THEN BEGIN
    //Copy resources
    COPY ~%worldmap_res_loc%/icons/nwnicons.bam~ override
    COPY ~%worldmap_res_loc%/map/nwnworld.mos~ override

    //Add language specific bams
    INCLUDE ~%MOD_FOLDER%/lib/ADD_MAP_ICONS.tpa~
    LAF ADD_MAP_ICONS
      INT_VAR
        icon_index = 0 // sequence (cycle) number of your first icon in your .bam, indexed from 0
      STR_VAR
        path_to_icons = EVAL ~%worldmap_append_bam_loc%/nwnicon1.bam~ // full path to the .bam file containing your icons, e.g. ~mymod/bam/mapicons.bam~
        worldmap = EVAL ~%worldmap_wmp_name%~ // which .wmp file should these icons be associated with, e.g. ~worldm25~
      RET
        new_icon_index // sequence number of your first icon in the new .bam - use this when you patch the worldmap
    END

    COPY ~%worldmap_res_loc%/areas.tbl~ ~%worldmap_res_loc%~
      FOR (cnt=0;cnt<2;cnt=cnt+1) BEGIN
        SET BAM_ANIM = new_icon_index + cnt
        REPLACE_TEXTUALLY ~new#%cnt%~ ~%BAM_ANIM%~
      END
  END

  ACTION_IF GAME_IS ~bg2ee eet~ THEN BEGIN
    //BAMv2 worldmap icons
    COPY ~%worldmap_res_loc%/icons/nwnicons.bam~ override
      LPF UPDATE_PVRZ_INDICES
        RET
          original_base_index
          new_base_index
      END
    PATCH_PRINT ~original_base_index: %original_base_index%; new_base_index: %new_base_index%~
    ACTION_IF original_base_index >= 0 AND new_base_index >= 0 BEGIN
      ACTION_BASH_FOR ~%worldmap_res_loc%/icons~ ~.+\.pvrz~ BEGIN
        LAF INSTALL_PVRZ
          INT_VAR
            original_base_index
            new_base_index
          STR_VAR
            source_file = EVAL ~%BASH_FOR_FILESPEC%~
        END
      END
    END ELSE FAIL ~Couldn't install PVRZ files~

    //BAMv2 worldmap canvas
    COPY ~%worldmap_res_loc%/map/nwnworld.mos~ override
      LPF UPDATE_PVRZ_INDICES
        RET
          original_base_index
          new_base_index
      END
    PATCH_PRINT ~original_base_index: %original_base_index%; new_base_index: %new_base_index%~
    ACTION_IF original_base_index >= 0 AND new_base_index >= 0 BEGIN
      ACTION_BASH_FOR ~%worldmap_res_loc%/map~ ~.+\.pvrz~ BEGIN
        LAF INSTALL_PVRZ
          INT_VAR
            original_base_index
            new_base_index
          STR_VAR
            source_file = EVAL ~%BASH_FOR_FILESPEC%~
        END
      END
    END ELSE FAIL ~Couldn't install PVRZ files~

    //Add language specific bams (v2)
    INCLUDE ~%MOD_FOLDER%/lib/ADD_MAP_ICONS_EE.tpa~
    LAF ADD_MAP_ICONS_EE
      INT_VAR
        icon_index = 0 // sequence (cycle) number of your first icon in your .bam, indexed from 0
      STR_VAR
        path_to_icons = EVAL ~%worldmap_append_bam_loc%/nwnicon2.bam~ // full path to the .bam file containing your icons, e.g. ~mymod/bam/mapicons.bam~
        patch_to_pvrz = EVAL ~%worldmap_append_bam_loc%~ // directory patch to .pvrz files associated with your merging .bam icon, e.g. ~mymod/pvrz~
        worldmap = EVAL ~%worldmap_wmp_name%~ // which .wmp file should these icons be associated with, e.g. ~worldm25~
      RET
        new_icon_index // sequence number of your first icon in the new .bam - use this when you patch the worldmap
    END

    COPY ~%worldmap_res_loc%/areas.tbl~ ~%worldmap_res_loc%~
      FOR (cnt=0;cnt<2;cnt=cnt+1) BEGIN
        SET BAM_ANIM = new_icon_index + cnt
        REPLACE_TEXTUALLY ~new#%cnt%~ ~%BAM_ANIM%~
      END
  END

  //Put areas and links on the worldmap
  INCLUDE ~%MOD_FOLDER%/lib/add_worldmap_tbl.tpa~ 
  LAF ADD_WORLDMAP_TBL
    INT_VAR
      verbose = 1
      inclSv = 0
    STR_VAR
      path_to_areas = EVAL ~%worldmap_res_loc%/areas.tbl~
      path_to_links = EVAL ~%worldmap_res_loc%/links.tbl~
      path_to_trans = EVAL ~%MOD_FOLDER%/tra/autotra/%LANGUAGE%/worldmap.tra~
      worldmap = EVAL ~%worldmap_wmp_name%~
  END

  END
  DEFAULT FAIL ~Incorrect input~ END
END