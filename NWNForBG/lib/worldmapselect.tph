//Choose between placing icons on BP-Worldmap or installing the new NWN Map

DEFINE_ACTION_FUNCTION WORLDMAP_SELECT
  STR_VAR
    worldmap_var = ""
BEGIN
  //BP-Worldmap
  ACTION_MATCH "%worldmap_var%" WITH bp_worldmap BEGIN
    PRINT ~Adding icons on BP Worldmap (don't forget to install it later!)~
    ACTION_IF FILE_EXISTS ~Worldmap/map_mods_areas.tbl~ THEN BEGIN
      COPY ~Worldmap/map_mods_areas.tbl~ ~Worldmap~ APPEND_FILE ~NWNForBG/Worldmap/areas.tbl~
      COPY ~Worldmap/map_mods_trans.tra~ ~Worldmap~ APPEND_FILE ~%tra_loc%/%LANGUAGE%/worldmap.tra~
      ACTION_IF FILE_EXISTS ~Worldmap/map_mods_links.tbl~ THEN BEGIN
        COPY ~Worldmap/map_mods_links.tbl~ ~Worldmap~ APPEND_FILE ~NWNForBG/Worldmap/links.tbl~
      END ELSE BEGIN
        COPY ~NWNForBG/Worldmap/links.tbl~ ~Worldmap/map_mods_links.tbl~
      END
    END ELSE BEGIN
      MKDIR ~Worldmap~
      COPY ~NWNForBG/Worldmap/areas.tbl~ ~Worldmap/map_mods_areas.tbl~
      COPY ~NWNForBG/Worldmap/links.tbl~ ~Worldmap/map_mods_links.tbl~
      COPY ~%tra_loc%/%LANGUAGE%/worldmap.tra~ ~Worldmap/map_mods_trans.tra~
    END
  END

  //New Worldmap for NWN Locations
  nwn_worldmap BEGIN
    //Assign variables
    ACTION_IF GAME_IS ~bg2 tob bgt~ THEN BEGIN
      OUTER_SPRINT worldmap_wmp_name worldmap
      OUTER_SPRINT worldmap_res_loc "%MOD_FOLDER%/worldmap/og"
      ACTION_IF FILE_EXISTS EVAL ~%MOD_FOLDER%/tra/%LANGUAGE%/worldmap/nwnicon1.bam~ THEN BEGIN
        OUTER_SPRINT worldmap_bam_append_loc ~%MOD_FOLDER%/tra/%LANGUAGE%/worldmap~
      END ELSE BEGIN
        OUTER_SPRINT worldmap_bam_append_loc ~%MOD_FOLDER%/tra/english/worldmap~
      END
    END
    ACTION_IF GAME_IS ~bg2ee eet~ THEN BEGIN
      OUTER_SPRINT worldmap_wmp_name nwnworld
      OUTER_SPRINT worldmap_res_loc "%MOD_FOLDER%/worldmap/ee"
      ACTION_IF FILE_EXISTS EVAL ~%MOD_FOLDER%/tra/%LANGUAGE%/worldmap/nwnicon2.bam~ THEN BEGIN
        OUTER_SPRINT worldmap_bam_append_loc ~%MOD_FOLDER%/tra/%LANGUAGE%/worldmap~
      END ELSE BEGIN
        OUTER_SPRINT worldmap_bam_append_loc ~%MOD_FOLDER%/tra/english/worldmap~
      END
    END

  //Copy WMAP file
  COPY ~%MOD_FOLDER%/worldmap/nwnworld.wmp~ ~override/%worldmap_wmp_name%.wmp~

  //Copy map resources to the workspace folder
  COPY ~%worldmap_res_loc%~ ~%workspace%/worldmap~
       ~%worldmap_res_loc%/icons~ ~%workspace%/worldmap/icons~
       ~%worldmap_res_loc%/map~ ~%workspace%/worldmap/map~
       ~%worldmap_bam_append_loc%~ ~%workspace%/worldmap/bam_append~

  //Making dummy files to bypass ADD_MAP_ICONS script protection
  COPY_EXISTING ~AR0011.ARE~ ~override/nwinfo01.are~
  COPY_EXISTING ~AR0011.ARE~ ~override/nwinfo02.are~

  ACTION_IF GAME_IS ~bg2 tob bgt~ THEN BEGIN
    //Copy resources
    PRINT ~Replacing default worldmap with the %worldmap_wmp_name%~
    COPY ~%workspace%/worldmap/icons/nwnicons.bam~ override
    COPY ~%workspace%/worldmap/map/nwnworld.mos~ override

    //Add language specific bams
    INCLUDE ~%MOD_FOLDER%/lib/ADD_MAP_ICONS.tpa~
    LAF ADD_MAP_ICONS
      INT_VAR
        icon_index = 0 // sequence (cycle) number of your first icon in your .bam, indexed from 0
      STR_VAR
        path_to_icons = EVAL ~%workspace%/worldmap/bam_append/nwnicon1.bam~ // full path to the .bam file containing your icons, e.g. ~mymod/bam/mapicons.bam~
        worldmap = EVAL ~%worldmap_wmp_name%~ // which .wmp file should these icons be associated with, e.g. ~worldm25~
      RET
        new_icon_index // sequence number of your first icon in the new .bam - use this when you patch the worldmap
    END

    COPY ~%workspace%/worldmap/areas.tbl~ ~%workspace%/worldmap~
      FOR (cnt=0;cnt<2;cnt=cnt+1) BEGIN
        SET BAM_ANIM = new_icon_index + cnt
        REPLACE_TEXTUALLY ~new#%cnt%~ ~%BAM_ANIM%~
      END
  END

  ACTION_IF GAME_IS ~bg2ee eet~ THEN BEGIN
    PRINT ~Installing new worldmap: %worldmap_wmp_name%~
    //BAMv2 worldmap icons
    COPY ~%workspace%/worldmap/icons/nwnicons.bam~ override
      LPF UPDATE_PVRZ_INDICES
        RET
          original_base_index
          new_base_index
      END
    PATCH_PRINT ~original_base_index: %original_base_index%; new_base_index: %new_base_index%~
    ACTION_IF original_base_index >= 0 AND new_base_index >= 0 BEGIN
      ACTION_BASH_FOR ~%workspace%/worldmap/icons~ ~.+\.pvrz~ BEGIN
        LAF INSTALL_PVRZ
          INT_VAR
            original_base_index
            new_base_index
          STR_VAR
            source_file = EVAL ~%BASH_FOR_FILESPEC%~
        END
      END
    END ELSE FAIL ~Couldn't install PVRZ files~

    //BAMv2 worldmap canvas
    COPY ~%workspace%/worldmap/map/nwnworld.mos~ override
      LPF UPDATE_PVRZ_INDICES
        RET
          original_base_index
          new_base_index
      END
    PATCH_PRINT ~original_base_index: %original_base_index%; new_base_index: %new_base_index%~
    ACTION_IF original_base_index >= 0 AND new_base_index >= 0 BEGIN
      ACTION_BASH_FOR ~%workspace%/worldmap/map~ ~.+\.pvrz~ BEGIN
        LAF INSTALL_PVRZ
          INT_VAR
            original_base_index
            new_base_index
          STR_VAR
            source_file = EVAL ~%BASH_FOR_FILESPEC%~
        END
      END
    END ELSE FAIL ~Couldn't install PVRZ files~

    //Add language specific bams (v2)
    INCLUDE ~%MOD_FOLDER%/lib/ADD_MAP_ICONS_EE.tpa~
    LAF ADD_MAP_ICONS_EE
      INT_VAR
        icon_index = 0 // sequence (cycle) number of your first icon in your .bam, indexed from 0
      STR_VAR
        path_to_icons = EVAL ~%workspace%/worldmap/bam_append/nwnicon2.bam~ // full path to the .bam file containing your icons, e.g. ~mymod/bam/mapicons.bam~
        patch_to_pvrz = EVAL ~%workspace%/worldmap/bam_append~ // directory patch to .pvrz files associated with your merging .bam icon, e.g. ~mymod/pvrz~
        worldmap = EVAL ~%worldmap_wmp_name%~ // which .wmp file should these icons be associated with, e.g. ~worldm25~
      RET
        new_icon_index // sequence number of your first icon in the new .bam - use this when you patch the worldmap
    END

    COPY ~%workspace%/worldmap/areas.tbl~ ~%workspace%/worldmap~
      FOR (cnt=0;cnt<2;cnt=cnt+1) BEGIN
        SET BAM_ANIM = new_icon_index + cnt
        REPLACE_TEXTUALLY ~new#%cnt%~ ~%BAM_ANIM%~
      END
  END

  //Put areas and links on the worldmap
  INCLUDE ~%MOD_FOLDER%/lib/add_worldmap_tbl.tpa~ 
  LAF ADD_WORLDMAP_TBL
    INT_VAR
      verbose = 1
      inclSv = 0
    STR_VAR
      path_to_areas = EVAL ~%workspace%/worldmap/areas.tbl~
      path_to_links = EVAL ~%workspace%/worldmap/links.tbl~
      path_to_trans = EVAL ~%tra_loc%/%LANGUAGE%/worldmap.tra~
      worldmap = EVAL ~%worldmap_wmp_name%~
  END

  END
  DEFAULT FAIL ~Incorrect input~ END
END
