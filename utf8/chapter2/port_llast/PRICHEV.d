// Порт-Лласт Склад в доках   Причев    Квест - уговорить капитана Мунга взять груз в Невервинтер

BEGIN ~PRICHEV~

IF WEIGHT #0 /* Triggers after states #: 9 even though they appear after this state */
~  Global("PrichevQuest","GLOBAL",0)Global("KnowPrichev","GLOBAL",0)Global("KnowCaptain","GLOBAL",0)~ THEN BEGIN 0 // from:
  SAY ~Послушайте, мне не хотелось бы вас обижать - споры с капитаном Мунгом выматывают. Так вы уж извините меня, но я не хочу разговаривать, ладно? Я тут гружу кое-какие запасы.~
  IF ~~ THEN REPLY ~Кто ты?~ GOTO 1
  IF ~~ THEN REPLY ~Что у вас произошло с тем парнем?~ GOTO 2
  IF ~~ THEN REPLY ~До свидания.~ GOTO 3
END

IF ~~ THEN BEGIN 1 // from: 0.1
  SAY ~Да вам-то что, а? Ох... простите. Звать меня Причев - и я сейчас несколько замотан, если вы не заметили. Я не хотел бы обременять вас своими проблемами, но... в общем, выговориться бы мне не мешало. И кто знает - может, вы сможете помочь?~
  IF ~~ THEN REPLY ~Продолжай. Я хочу об этом услышать.~ GOTO 2
  IF ~~ THEN REPLY ~Прости - все это не очень интересно. Счастливо.~ GOTO 4
END

IF ~~ THEN BEGIN 2 // from: 0.2
  SAY ~У меня есть два груза - один направляется в Уотердип, а другой в Невервинтер. Товар для Уотердипа стоит больше, но невервинтерский груз благотворительный - это помощь, которую собрали жители Порт-Лласта. У капитана Мунга есть место только для одного груза. Конечно, он хочет получить деньги за более ценный товар - но народу в Невервинтере действительно *нужен* этот груз чтобы облегчить их положение.~
  IF ~~ THEN REPLY ~Я поговорю с капитаном о доставке груза в Невервинтер.~ GOTO 5
  IF ~~ THEN REPLY ~Не можете ли вы придти к какому-нибудь соглашению?~ GOTO 6
  IF ~ Global("KnowCaptain","GLOBAL",1)~ THEN REPLY ~У меня был уже разговор с капитаном.~ GOTO 7
  IF ~~ THEN REPLY ~Прости - все это не очень интересно. Счастливо.~ GOTO 4
END

IF ~~ THEN BEGIN 3 // from: 0.3
  SAY ~Да. Удачи. Попробую еще раз уговорить эту жадную свинью сделать что-нибудь для людей.~
  IF ~~ THEN EXIT
END

IF ~~ THEN BEGIN 4 // from: 1.2 2.4
  SAY ~Я понимаю. Это действительно ни в какой мере не ваше дело. Извините, что пытался втянуть вас в эту историю.~
  IF ~~ THEN DO ~SetGlobal("KnowPrichev","GLOBAL",1)~ EXIT
END

IF ~~ THEN BEGIN 5 // from: 2.1
  SAY ~Спасибо. Я не беру с вас никаких обещаний, но если вы заставите его передумать, ну... это было бы правильно.~
  IF ~~ THEN DO ~SetGlobal("KnowPrichev","GLOBAL",1)~ EXIT
END

IF ~~ THEN BEGIN 6 // from: 2.2
  SAY ~Хорошо бы так, но на самом деле - тут все однозначно. Народ в Порт-Лласте тяжело работал, чтобы помочь Невервинтеру. Я лишь хотел бы, чтобы эти усилия не пропали даром.~
  IF ~~ THEN REPLY ~Я поговорю с капитаном о доставке груза в Невервинтер.~ GOTO 5
  IF ~  Global("KnowCaptain","GLOBAL",1)~ THEN REPLY ~У меня был уже разговор с капитаном.~ GOTO 7
  IF ~~ THEN REPLY ~Прости - все это не очень интересно. Счастливо.~ GOTO 4
END

IF ~~ THEN BEGIN 7 // from: 2.3
  SAY ~Да, и что он сказал? Возьмет он, в конце концов, этот невервинтерский груз? Подумайте только об этих отчаявшихся душах. Пожалуйста, скажите мне, что вам удалось уговорить его образумиться.~
  IF ~  Global("PrichevQuest","GLOBAL",0)~ THEN REPLY ~Пока нет. Я еще раз с ним поговорю.~ GOTO 8
  IF ~~ THEN REPLY ~Я думаю, он прав. Вам следует отдать ему уотердипский груз.~ GOTO 9
  IF ~~ THEN REPLY ~Прости - все это не очень интересно. Счастливо.~ GOTO 4
END

IF ~~ THEN BEGIN 8 // from: 7.1
  SAY ~Знаю, я, наверное, выгляжу раздраженным, но вся эта ситуация так на меня влияет. Пожалуйста, поговорите с ним. Я верю, у вас получится.~
  IF ~~ THEN DO ~SetGlobal("KnowPrichev","GLOBAL",1)~ EXIT
END

IF ~~ THEN BEGIN 9 // from: 7.2
  SAY ~Вы серьезно? Вы что, не понимаете, какова ситуация? Тут в Порт-Лласте все скинулись, чтобы помочь гражданам Невервинтера - и этот груз должен быть доставлен. Он чрезвычайно облегчит невервинтерцам жизнь. Я понимаю, как важны деньги, но деньги еще не все, знаете ли. Это доброе дело!~
  IF ~~ THEN REPLY ~Теперь я понимаю. Я поговорю с ним..~ GOTO 8
  IF ~~ THEN REPLY ~Вам легко говорить. Вам-то в любом случае заплатят.~ GOTO 10
  IF ~~ THEN REPLY ~Может, немного золота заставит вас передумать?~ GOTO 11
END

IF ~~ THEN BEGIN 10 // from: 9.2
  SAY ~Мне наплевать на ваши доводы или обвинения. Я не думаю о бизнесе - я думаю о помощи. Если не можете мне помочь, оставьте меня в покое. Что-нибудь еще? У меня есть груз, который должен попасть в Невервинтер, и никаких признаков корабля, который мог бы его доставить. Я займусь делом, не возражаете?~
  IF ~~ THEN REPLY ~Теперь я понимаю. Я поговорю с ним..~ GOTO 8
  IF ~~ THEN REPLY ~Может, немного золота заставит вас передумать?~ GOTO 11
  IF ~~ THEN REPLY ~Прости - все это не очень интересно. Счастливо.~ GOTO 4
END

IF ~~ THEN BEGIN 11 // from: 9.3
  SAY ~Вы, без сомнения, можете найти лучшее применение для своего золота, чем разбрасываться им ради чьей-то чужой выгоды? Идите-ка вы со своими взятками в другое место, ладно? Что-нибудь еще? У меня есть груз, который должен попасть в Невервинтер, и никаких признаков корабля, который мог бы его доставить. Я займусь делом, не возражаете?~
  IF ~~ THEN REPLY ~Теперь я понимаю. Я поговорю с ним..~ GOTO 8
  IF ~~ THEN REPLY ~Прости - все это не очень интересно. Счастливо.~ GOTO 4
END

// ------------------------------------

IF WEIGHT #1 /* Triggers after states #: 9 even though they appear after this state */
~  Global("PrichevQuest","GLOBAL",0)Global("KnowPrichev","GLOBAL",1)~ THEN BEGIN 12 // from:
  SAY ~Вам, кажется, хотелось поговорить о чем-то другом? Извините, если я кажусь рассеянным - я просто не могу не думать об этом своем споре с капитаном Мунгом.~
  IF ~~ THEN REPLY ~Скажи еще раз, что у вас произошло с тем парнем?~ GOTO 2
  IF ~~ THEN REPLY ~Я поговорю с капитаном о доставке груза в Невервинтер.~ GOTO 5
  IF ~~ THEN REPLY ~Я думаю, капитан прав. Вам следует отдать ему уотердипский груз.~ GOTO 9
  IF ~~ THEN REPLY ~Прости - все это не очень интересно. Счастливо.~ GOTO 4
END

// ----------------------------------------------------

IF WEIGHT #2 /* Triggers after states #: 9 even though they appear after this state */
~  Global("PrichevQuest","GLOBAL",0)Global("KnowPrichev","GLOBAL",0)Global("KnowCaptain","GLOBAL",1)~ THEN BEGIN 13 // from:
  SAY ~Слушайте, я видел, вы говорили с капитаном Мунгом. Уверен, он вам много чего про меня наврал. Просто выслушайте и мою позицию, прежде чем делать заявления, ладно?~
  IF ~~ THEN REPLY ~Слушайте, а как насчет того, чтоб отдать капитану уотердипский груз?~ GOTO 9
  IF ~~ THEN REPLY ~Кто ты?~ GOTO 1
  IF ~~ THEN REPLY ~Что у вас произошло с тем парнем?~ GOTO 2
  IF ~~ THEN REPLY ~Прости - все это не очень интересно. Счастливо.~ GOTO 4
END

// ----------------------------------------------------------------------------------------------------------

IF WEIGHT #3 /* Triggers after states #: 9 even though they appear after this state */
~  Global("PrichevQuest","GLOBAL",1)~ THEN BEGIN 14 // from:
  SAY ~Я даже не знаю, что случилось, но у меня наконец-то кое-какие товары сейчас отправляются в Невервинтер. Позвольте мне отблагодарить ваc, вот, здесь 400 золотых.~
  IF ~~ THEN REPLY ~Спасибо, этого достаточно.~ GOTO 15
  IF ~  CheckStatGT(LastTalkedToBy(Myself),11,CHR)~ THEN REPLY ~Это не то спасибо. Это стоит чуть побольше.~ GOTO 16
  IF ~  CheckStatLT(LastTalkedToBy(Myself),12,CHR)~ THEN REPLY ~Это не то спасибо. Это стоит чуть побольше.~ GOTO 17
  IF ~~ THEN REPLY ~Вам бы дать мне побольше, а то я опять пойду поговорю с капитаном.~ GOTO 18
  IF ~~ THEN REPLY ~Я не возьму денег, помочь Невервинтеру - это правильно.~ GOTO 19
END

IF ~~ THEN BEGIN 15 // from: 14.1
  SAY ~Спасибо за вашу заботу, <SIRMAAM>. Теперь я займусь отправкой груза, до свидания.~
  IF ~~ THEN DO ~SetGlobal("PrichevQuest","GLOBAL",3)GiveGoldForce(400)AddexperienceParty(20000)~ EXIT
END

IF ~~ THEN BEGIN 16 // from: 14.2
  SAY ~Да, я думаю. Вам довелось славно потрудиться, да и я в хорошем настроении. Хорошо, примите эти 800 золотых вместе с моей благодарностью.~
  IF ~~ THEN DO ~SetGlobal("PrichevQuest","GLOBAL",3)GiveGoldForce(800)AddexperienceParty(20000)~ EXIT
END

IF ~~ THEN BEGIN 17 // from: 14.3
  SAY ~Порт-Лласту уже и так дорого обошлась подготовка этих припасов. Требовать большего - это уже слишком. Я вынужден просить вас поумерить запросы.~
  IF ~~ THEN REPLY ~Спасибо, этого достаточно.~ GOTO 15
  IF ~~ THEN REPLY ~Раскошеливайся, или я опять пойду поговорю с капитаном.~ GOTO 18
END

IF ~~ THEN BEGIN 18 // from: 14.4
  SAY ~Я так и знал, что такие вещи не делаются из чистой доброты. За всем в этом мире стоит чья-то выгода, не так ли? Вот, возьмите эти деньги. По крайней мере, я могу быть уверен в том, что груз доберется до Невервинтера.~
  IF ~~ THEN DO ~SetGlobal("PrichevQuest","GLOBAL",3)GiveGoldForce(800)AddexperienceParty(20000)ReputationInc(-1)~ EXIT
END

IF ~~ THEN BEGIN 19 // from: 14.5
  SAY ~И в такой день - такое проявление доброты... о, моя вера в человечество окрепла. Спасибо за вашу заботу, <SIRMAAM>. Спасибо.~
  IF ~~ THEN DO ~SetGlobal("PrichevQuest","GLOBAL",3)AddexperienceParty(20000)ReputationInc(1)~ EXIT
END

// ---------------------------------------------------------------------------------------------------------

IF WEIGHT #4 /* Triggers after states #: 9 even though they appear after this state */
~  Global("PrichevQuest","GLOBAL",2)~ THEN BEGIN 20 // from:
  SAY ~Что?! Я должен был понять, что не стоит вам доверять. Из-за вас нужные Невервинтеру товары не попадут в город.~
  IF ~~ THEN EXIT
END

// ---------------------------------------------------------------------------------------------------------

IF WEIGHT #5 /* Triggers after states #: 9 even though they appear after this state */
~  Global("PrichevQuest","GLOBAL",3)~ THEN BEGIN 21 // from:
  SAY ~Ну, в конце концов, день удался. Порт-Лласт объединился, и мы собрали немного пожертвований для Невервинтера. Мир не без добрых людей, все же.~
  IF ~~ THEN EXIT
END
